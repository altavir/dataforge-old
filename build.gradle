buildscript {
    repositories {
        jcenter()

    }
    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    }
}

apply plugin: 'idea'
apply plugin: "com.jfrog.artifactory"
apply plugin: 'maven-publish'

allprojects{
    group = 'hep.dataforge'
    version = '0.4.0 - SNAPSHOT'
}

subprojects{
    if(it.name != "site") {
        apply plugin: "com.jfrog.artifactory"
        apply plugin: 'maven-publish'
        apply plugin: 'java'

        [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

        sourceCompatibility = 1.8
        targetCompatibility = 1.8

        repositories {
            jcenter()
            mavenCentral()
            maven { url "https://jitpack.io" }
        }

        dependencies {
            testCompile 'org.codehaus.groovy:groovy-all:2.4.+'
            testCompile group: 'junit', name: 'junit', version: '4.+'
            testCompile "org.spockframework:spock-core:1.0-groovy-2.+"
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

if(file('properties.gradle').exists()) {
    artifactory {
        contextUrl = "http://npm.mipt.ru:8081/artifactory"
        //The base Artifactory URL if not overridden by the publisher/resolver
        publish {
            repository {
                repoKey = 'gradle-dev-local'
                username = "${artifactory.user}"
                password = "${artifactory.pass}"
                maven = true

            }
            defaults {
                publications('mavenJava')
            }
        }
        resolve {
            repository {
                repoKey = 'gradle-dev'
                username = "${artifactory.user}"
                password = "${artifactory.pass}"
                maven = true

            }
        }
    }
}
